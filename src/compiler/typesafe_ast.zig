const std = @import("std");
const root = @import("root");
const mem = std.mem;
const Allocator = mem.Allocator;
const Type = root.Type;

/// This AST is assumed to be type safe
/// since it is generated by the analyzer.
pub const Program = struct {
    // functions: []const Function,
    body: std.ArrayList(Stmt),

    pub fn deinit(self: Program, allocator: Allocator) void {
        for (self.body.items) |body| {
            body.deinit(allocator);
        }
        self.body.deinit();
    }
};

pub const Stmt = union(enum) {
    Expr: Expr,
    Var: struct {
        name: []const u8,
        value: ?Expr,
        tp: Type,
        global: bool = false,
    },
    Const: struct {
        name: []const u8,
        value: ?Expr,
        tp: Type,
        global: bool = false,
    },

    pub fn create(allocator: Allocator, value: Stmt) Allocator.Error!Stmt {
        const result = try allocator.create(Stmt);
        result.* = value;
        return result;
    }

    pub fn get_type(self: Stmt) Type {
        return switch (self) {
            .Expr => |expr| expr.get_type(),
            .Var => |vr| vr.tp,
            .Const => |con| con.tp,
        };
    }

    pub fn deinit(self: Stmt, allocator: Allocator) void {
        switch (self) {
            .Expr => |expr| expr.deinit(allocator),
            .Var => |vr| {
                if (vr.value) |value| {
                    value.deinit(allocator);
                }
            },
            .Const => |con| {
                if (con.value) |value| {
                    value.deinit(allocator);
                }
            },
        }
    }
};

pub const Expr = union(enum) {
    pub const AssignNode = struct {
        vr: *const Expr,
        value: *const Expr,
        tp: Type,
    };
    pub const BinaryNode = struct {
        left: *const Expr,
        right: *const Expr,
        op: []const u8,
        tp: Type,
    };
    pub const UnaryNode = struct {
        op: []const u8,
        right: *const Expr,
        tp: Type,
    };
    pub const StringNode = struct {
        v: []const u8,
        tp: Type,
    };
    pub const ConstantNode = struct {
        v: []const u8,
        tp: Type,
    };
    Binary: BinaryNode,
    Unary: UnaryNode,
    // AssignGroup: []Assign,
    Assign: AssignNode,
    String: StringNode,
    Constant: ConstantNode,

    pub fn create(allocator: Allocator, value: Expr) Allocator.Error!*Expr {
        const result = try allocator.create(Expr);
        result.* = value;
        return result;
    }

    pub fn get_type(self: Expr) Type {
        return switch (self) {
            .Binary => |bin| bin.tp,
            .Unary => |un| un.tp,
            .Assign => |a| a.tp,
            .String => |str| str.tp,
            .Constant => |con| con.tp,
        };
    }

    pub fn is_assignment(self: Expr) bool {
        return self == .Assign;
    }

    pub fn deinit(self: Expr, allocator: Allocator) void {
        switch (self) {
            .Binary => |bin| {
                bin.left.deinit(allocator);
                bin.right.deinit(allocator);

                allocator.destroy(bin.left);
                allocator.destroy(bin.right);
            },
            .Unary => |un| {
                un.right.deinit(allocator);

                allocator.destroy(un.right);
            },
            .Assign => |a| {
                a.vr.deinit(allocator);
                a.value.deinit(allocator);

                allocator.destroy(a.vr);
                allocator.destroy(a.value);
            },
            .String => {},
            .Constant => {},
        }
    }
};

pub const Function = struct {
    name: []const u8,
    args: []const Arg,
    body: []const Stmt,
    is_local: bool,

    const Arg = struct {
        name: []const u8,
    };
};
